archetype (adl_version=1.4)
	openEHR-EHR-CLUSTER.medication_item.v1

concept
	[at0000]	-- Medication item
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Dr Ian McNicoll">
		["organisation"] = <"Ocean Informatics, UK">
		["email"] = <"ian.mcnicoll@oceaninformatics.com">
		["date"] = <"2013-01-30">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"The Medication Item Cluster records details of a medication, dosage, administration and dispensing instructions. 
The model is closely aligned with the SCI-XML, dm+d, RCP medication record guidance, HSCIC Dose syntax and GP2GP medication models. It is intended to support both ‘product-based’ prescribing, used in community and most out-patient settings; and ‘dose-based’ prescribing , generally used in in-patient settings.
e.g.
 Product-based prescribing “Citalopram tab 20mg, 1 tab daily”,
 Dose-based prescribing  ”Citalopram, oral, 20 mg once daily”.

The archetype may be used to record core details of a medication order, medication action or medication item summary e.g in an unscheduled care summary (KIS, SCR, IHR), referral or discharge letter, or to underpin an API call which returns a medication summary.

The archetype may also be used to record discrete medication 'events', the normal approach within an individual application or shared medication record, where each phase of oredering or supplying the medication is stored as separate record.

The differences between hospital 'dose-based' prescribing and ambulatory 'product-based' prescribing, currently limit the degree to which the medication record can be made interoperable across care settings, particularly because of the potential complexity of dosage instructions.

For clinical safety reasons, fully automated information exchange at transitions of care, without human oversight,  is probably undesirable, and in spite of the limitations, the use of a common, structured medication record, will add substantial benefit.">
			use = <"This archetype will always be used within the context of a parent Entry archetype, such as 'Medication Order' or 'Medication Action' and is designed for outpatient and ambulatory care settings. It may also be used at transitions of care to facilitate medicines reconciliation.

Use of the archetype will vary depending on the care setting responsible ...

A. Ambulatory care settings (GP, Community, Mental health, some outpatient settings):
The medication Name is normally represented by a dm+d product-code (VMP/AMP), which will include the product Strength and Form, and Route is normally not specified. Dosage is generally expressed as a single text string and should be carried in 'Dose Description'. 

B. Hospital settings (Inpatient and some outpatient settings):
The medication Name is normally represented by a dm+d code (VTM), Form and Route are normally specified explicitly  and Dosage is generally expressed as Dose / Frequency which should be carried in 'Dose Instructions' as simple text strings. 

More extensive adoption of the HSCIC Dose Syntax would allow Dose and Frequency to be captured in a more structured and computable form but this is recognised as being potentially complex to implement, at least in complete form, safely and consistently across all care settings.">
			keywords = <"medication", "prescription", "dosage", "administration", "dispensing", "drug", "therapeutics", "pharmacology", "pharmacy">
			misuse = <"This archetype has not been designed for use in an inpatient setting, other than at transitions of care to facilitate medicines reconciliation at patient transfer from/to a community setting. It is possible that the archetype would be suitable for inpatient use with some modification or specialisation.">
			copyright = <"© Clinical Models UK">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_contributors = <"Steve Bentley, HSCIC, United Kingdom", "Colin Brown, SCIMP, United Kingdom", "Richard Cottrell, NHS Ayrshire and Arran, United Kingdom", "RCP Health Informatics Unit, UK", "Dr John Williams, GP2GP project, UK", "Dr Leo Fogarty, GP2GP project, UK", "Heather Leslie, Ocean Informatics, Australia", "Dawn MacDermid, United Kingdom", "Ian McNicoll, Ocean Informatics, United Kingdom (Editor)", "Barry Melia, NHS Lothian, United Kingdom", "Mike Robinson, INPS, United Kingdom", "Laura Sato, Health and Social Care Information Centre, United Kingdom">
	other_details = <
		["references"] = <"HSCIC Messaging Implementation Manual (GP2GP messages) http://www.uktcregistration.nss.cfh.nhs.uk/trud3

NHS Scotland SCI-XML (SCI-Gateway, ECS, ePharmacy)  http://www.sci.scot.nhs.uk/products/gateway/gate_down_xml.htm

NHS Wales Informatics Service, Integrated Health Record http://www.wales.nhs.uk/nwis/page/52553

DM+D drug dictionary: http://www.dmd.nhs.uk/

Royal College of Physicians, Core Clinical Headings, http://www.rcplondon.ac.uk/projects/standards-core-clinical-information

Royal College of Physicians, Standards for structure and content of medications and medical device records: technical annex, http://www.rcplondon.ac.uk/sites/default/files/medication-and-medical-device-records-technical-annex-2013.pdf">
		["MD5-CAM-1.0.1"] = <"AA828181FA26AAB4484A0C2CB2C5B695">
	>

definition
	CLUSTER[at0000] matches {	-- Medication item
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001] matches {	-- Medication name
				value matches {
					DV_TEXT matches {*}
					DV_CODED_TEXT matches {
						defining_code matches {[ac0003]}		-- NHS dm+d AMP/AMPP/VMP/VMPP/VTM
					}
				}
			}
			ELEMENT[at0022] occurrences matches {0..1} matches {	-- Form
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {[ac0005]}		-- SNOMED-CT CFH::MedicationDoseForm
					}
				}
			}
			ELEMENT[at0002] occurrences matches {0..*} matches {	-- Route
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {[ac0001]}		-- RouteOfAdministration
					}
				}
			}
			ELEMENT[at0029] occurrences matches {0..1} matches {	-- Site
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {[ac0006]}		-- Medication administration site
					}
				}
			}
			CLUSTER[at0019] occurrences matches {0..1} matches {	-- Dose directions
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0003] occurrences matches {0..1} matches {	-- Dose directions description
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0020] occurrences matches {0..1} matches {	-- Dose strength description
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0021] occurrences matches {0..1} matches {	-- Dose frequency description
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0005] occurrences matches {0..*} matches {	-- Additional instruction
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
			ELEMENT[at0030] occurrences matches {0..1} matches {	-- Administration method
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0027] occurrences matches {0..*} matches {	-- Patient instruction
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0004] occurrences matches {0..*} matches {	-- Dispensing instruction
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0026] occurrences matches {0..1} matches {	-- Total daily dose quantity
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.medication_quantity_sct(-[a-zA-Z0-9_]+)*\.v1/}
			}
			ELEMENT[at0028] occurrences matches {0..1} matches {	-- Comment
				value matches {
					DV_TEXT matches {*}
				}
			}
		}
	}


ontology
	terminologies_available = <"SNOMED-CT", "dm+d">
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Medication item">
					description = <"Records details of a medication product, dosage and administration directions, for use within medication recommendation, order, administration and dispensation records.">
				>
				["at0001"] = <
					text = <"Medication name">
					description = <"Mandatory medication name coded using a SNOMEDCT/dm+d term where possible, allowing plain text for historical/patient reported items , extemporaneous preparations or those not registered in dm+d.">
					comment = <"e.g.“Citalopram tab 20mg”, \"Trimethoprim\"">
				>
				["at0002"] = <
					text = <"Route">
					description = <"Optional medication route, using SNOMED terms where possible. Not generally applicable to product-based medication. Should not be used to specify a specific administration site, for which a separate archetype is used e.g. The Route is 'intraocular' the Site may be 'Left eye'.">
					comment = <"e.g. \"Oral\", \"Intraocular\"">
				>
				["at0003"] = <
					text = <"Dose directions description">
					description = <"A single plain text phrase describing the entire medication dosage and administration directions, including dose quantity and medication frequency.">
					comment = <"e.g. “I tablet at night” or “20mg at 10pm”">
				>
				["at0004"] = <
					text = <"Dispensing instruction">
					description = <"Multiple plain text to record complex dispensing arrangements, particularly for Controlled Drug instalment dispensing.">
					comment = <"e.g. “Dispense weekly”">
				>
				["at0005"] = <
					text = <"Additional instruction">
					description = <"Additional multiple dosage or administration instructions as plain text. This may include guidance to the prescriber, patient or person administering the medication.">
					comment = <"e.g. “Omit morning dose on day of procedure”">
				>
				["at0019"] = <
					text = <"Dose directions">
					description = <"Structured details of medication dosage directions.">
				>
				["at0020"] = <
					text = <"Dose strength description">
					description = <"A plain text description of medication single dose amount.">
					comment = <"e.g. \"30 mg\" or \"2 tabs\"">
				>
				["at0021"] = <
					text = <"Dose frequency description">
					description = <"A plain text description of medication dose frequency.">
					comment = <"e.g. \"Twice a day\"">
				>
				["at0022"] = <
					text = <"Form">
					description = <"Form of the medicinal substance e.g capsules, tablets, liquid. Not normally required unless a specific form has been requested by the prescriber.">
					comment = <"e.g. \"Modified Release Capsules\"">
				>
				["at0026"] = <
					text = <"Total daily dose quantity">
					description = <"The total daily dose of this medication.  This is helpful for estimating optimal adherance to dosing guidance. It may be computed from product/dose strength and frequency or entered manually.">
				>
				["at0027"] = <
					text = <"Patient instruction">
					description = <"Multiple plain text instructions intended for patient or carer.">
				>
				["at0028"] = <
					text = <"Comment">
					description = <"An additional comment about the medication item as a whole e.g. 'Patient named supply', 'unlicensed medication', 'Foreign brand'.">
				>
				["at0029"] = <
					text = <"Site">
					description = <"The anatomical site at which the medication is to be administered.">
					comment = <"e.g. \"Left eye\"">
				>
				["at0030"] = <
					text = <"Administration method">
					description = <"The technique or method by which the medication is to be administered.">
				>
			>
		>
	>
	constraint_definitions = <
		["en"] = <
			items = <
				["ac0001"] = <
					text = <"RouteOfAdministration">
					description = <"NHS e-Prescribing route of administration subset.
Id : 353901000001136
Original Id : 30201000001137


">
				>
				["ac0003"] = <
					text = <"NHS dm+d AMP/AMPP/VMP/VMPP/VTM">
					description = <"Any AMP/VMP/VTM/AMPP/VMPP subsets from the dm+d terminology.

NHS dm+d AMP   ::352201000001139
NHS dm+d AMPP ::352401000001135
NHS dm+d VMP   ::352701000001133
NHS dm+d VMPP ::352301000001131
NHS dm+d VTM   ::352601000001138">
				>
				["ac0005"] = <
					text = <"SNOMED-CT CFH::MedicationDoseForm">
					description = <"SNOMED-CT CFH::DoseForm termset">
				>
				["ac0006"] = <
					text = <"Medication administration site">
					description = <"Any valid site for the administration of a medication.">
				>
			>
		>
	>
	constraint_bindings = <
		["SNOMED-CT"] = <
			items = <
				["ac0001"] = <terminology:SNOMED-CT?subset=%20353901000001136>
				["ac0005"] = <terminology:SNOMED-CT?subset=CFH%3A%3ADoseForm>
				["ac0006"] = <terminology:SNOMED-CT?subset=MedicationAdministrationSite>
			>
		>
		["dm+d"] = <
			items = <
				["ac0003"] = <terminology:dm%2Bd?subset=NHS_dm%2Bd>
			>
		>
	>
