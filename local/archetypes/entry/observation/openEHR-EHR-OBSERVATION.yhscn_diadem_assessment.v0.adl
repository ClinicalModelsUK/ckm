archetype (adl_version=1.4; uid=d85f4081-c34f-4402-84f3-b0ccea5221e0)
	openEHR-EHR-OBSERVATION.yhscn_diadem_assessment.v0

concept
	[at0000]	-- YHSCN - DiADeM assessment v0
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Hildegard Franke">
		["organisation"] = <"freshEHR Clinical Informatics Ltd.">
		["email"] = <"hildi@freshehr.com">
		["date"] = <"2016-02-26">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Local archetype for Yorkshire and the Humber Clinical Networks Dementia Management. This archetype forms part of the DiADeM (Diagnosing Advanced Dementia Mandate for care home settings) Tool.">
			use = <"Local use by Yorkshire and the Humber Clinical Networks for their dementia management programme.">
			keywords = <"diadem", "dementia">
			misuse = <"">
			copyright = <"© Clinical Models UK">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Ian McNicoll, freshEHR Clinical Informatics Ltd.", ...>
	other_details = <
		["current_contact"] = <"Hildegard Franke, freshEHR Clinical Informatics Ltd.<hildi@freshehr.com>">
		["revision"] = <"0.0.1-alpha">
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["build_uid"] = <"03e13a87-4e2c-4c70-b66c-a2bd543c5d38">
		["original_publisher"] = <"UK Clinical Models">
		["original_namespace"] = <"uk.org.clinicalmodels">
		["MD5-CAM-1.0.1"] = <"63AFC5A0DC2F27348FF3B4FA29FC123C">
		["custodian_namespace"] = <"uk.org.clinicalmodels">
		["custodian_organisation"] = <"UK Clinical Models">
	>

definition
	OBSERVATION[at0000] matches {	-- YHSCN - DiADeM assessment v0
		data matches {
			HISTORY[at0001] matches {	-- Event Series
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..1} matches {	-- Any event
						data matches {
							ITEM_TREE[at0003] matches {	-- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0004] occurrences matches {0..1} matches {	-- Functional impairment
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0006, 	-- No longer fully independent
													at0005]	-- Fully Independent
												}
											}
										}
									}
									ELEMENT[at0007] occurrences matches {0..1} matches {	-- Corroborating history
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0011, 	-- Corroborating history confirmed
													at0010]	-- Corroborating history NOT confirmed
												}
											}
										}
									}
									ELEMENT[at0008] occurrences matches {0..1} matches {	-- Investigations
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0012, 	-- Normal Dementia screening bloods
													at0013, 	-- Abnormal Dementia screening bloods
													at0023]	-- Screening bloods not done
												}
											}
										}
									}
									ELEMENT[at0024] occurrences matches {0..1} matches {	-- Reason why bloods not done
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0009] occurrences matches {0..1} matches {	-- Exclusion criteria
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0014, 	-- NO acute underlying cause to explain confusion
													at0015]	-- Acute underlying cause to explain confusion
												}
											}
										}
									}
									ELEMENT[at0022] occurrences matches {0..1} matches {	-- Interpretation
										value matches {
											DV_TEXT matches {*}
										}
									}
									CLUSTER[at0019] occurrences matches {0..1} matches {	-- Other assessment tool
										items cardinality matches {1..*; unordered} matches {
											ELEMENT[at0020] occurrences matches {0..1} matches {	-- Tool name
												value matches {
													DV_TEXT matches {*}
												}
											}
											ELEMENT[at0016] occurrences matches {0..1} matches {	-- Tool result
												value matches {
													DV_CODED_TEXT matches {
														defining_code matches {
															[local::
															at0017, 	-- Score indicates impairment
															at0018]	-- Score does not indicate impairment
														}
													}
												}
											}
											ELEMENT[at0021] occurrences matches {0..1} matches {	-- Link to original result
												value matches {
													DV_EHR_URI matches {*}
												}
											}
										}
									}
								}
							}
						}
						state matches {
							ITEM_TREE[at0025] matches {	-- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0026] occurrences matches {0..*} matches {	-- Confounding factors
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0027] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0028] occurrences matches {0..*} matches {	-- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"YHSCN - DiADeM assessment v0">
					description = <"Additional clinical information used to complete DiADeM assessment.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"A specific date and/or time which may be explicitly defined in a template or at run-time.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Functional impairment">
					description = <"Statement to confirm whether the patient is fully independent in relation to basic activities of daily living, washing, dressing, feeding and 
attending to own continence needs. The requirement of prompting or supervision of staff constitutes a loss of full independence.">
				>
				["at0005"] = <
					text = <"Fully Independent">
					description = <"The patient is fully independent in relation to basic activities of daily living.">
				>
				["at0006"] = <
					text = <"No longer fully independent">
					description = <"The patient is no longer fully independent in relation to the basic activities of daily living.">
				>
				["at0007"] = <
					text = <"Corroborating history">
					description = <"Statement whether history of gradual cognitive decline (typically for the last few years) is confirmed by care staff, relatives and medical records. Staff/relatives confirm that in their opinion the patient consistently demonstrates both functional and cognitive 
impairment.">
				>
				["at0008"] = <
					text = <"Investigations">
					description = <"Statement whether dementia screening bloods are normal (where clinically appropriate and patient consents to bloods). If patient lacks capacity to consent to bloods, a best interest decision must be made and documented accordingly.
 NB. If intracranial pathology (e.g. subdural haematoma, cerebral tumour) is suspected, referral for a brain scan may be appropriate. Otherwise where dementia is advanced, differential diagnosis is unlikely to affect patient management & a brain scan is unnecessary.">
				>
				["at0009"] = <
					text = <"Exclusion criteria">
					description = <"Statement whether there is acute underlying cause to explain confusion i.e. delirium (acute confusional state) has been excluded. Also confirmation whether mood disorder or psychosis is also confirmed or excluded.">
				>
				["at0010"] = <
					text = <"Corroborating history NOT confirmed">
					description = <"History of gradual cognitive decline is not confirmed by staff, relatives and medical records.">
				>
				["at0011"] = <
					text = <"Corroborating history confirmed">
					description = <"History of gradual cognitive decline is confirmed by staff, relatives and medical records.">
				>
				["at0012"] = <
					text = <"Normal Dementia screening bloods">
					description = <"Blood tests screening for other causes are normal.">
				>
				["at0013"] = <
					text = <"Abnormal Dementia screening bloods">
					description = <"Blood tests indicate other causes.">
				>
				["at0014"] = <
					text = <"NO acute underlying cause to explain confusion">
					description = <"Acute underlying causes to explain confusion have been excluded.">
				>
				["at0015"] = <
					text = <"Acute underlying cause to explain confusion">
					description = <"There are acute underlying causes to explain confusion.">
				>
				["at0016"] = <
					text = <"Tool result">
					description = <"Result of assessment using another assessment tool.">
				>
				["at0017"] = <
					text = <"Score indicates impairment">
					description = <"Assessment tool score indicates cognitive impairment.">
				>
				["at0018"] = <
					text = <"Score does not indicate impairment">
					description = <"Assessment tool score does not indicate cognitive impairment.">
				>
				["at0019"] = <
					text = <"Other assessment tool">
					description = <"Another assessment tool (other than 6 CIT Score) has been used.">
				>
				["at0020"] = <
					text = <"Tool name">
					description = <"Name of other assessment tool used.">
				>
				["at0021"] = <
					text = <"Link to original result">
					description = <"Patient record entry showing original score result.">
				>
				["at0022"] = <
					text = <"Interpretation">
					description = <"The overall interpretation of the assessment, i.e. a narrative statement of whether the assessment indicates that the patient suffers from dementia or whether the assessment indicates that the patient does not suffer from dementia.">
				>
				["at0023"] = <
					text = <"Screening bloods not done">
					description = <"Blood tests screening for other causes have not been done.">
				>
				["at0024"] = <
					text = <"Reason why bloods not done">
					description = <"Statement why screening blood tests have not been done, e.g. patient refusal.">
				>
				["at0025"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0026"] = <
					text = <"Confounding factors">
					description = <"Record any issues or factors that may impact on the overall assessment.">
				>
				["at0027"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0028"] = <
					text = <"Extension">
					description = <"Additional information required to capture local context or to align with other reference models/formalisms.">
					comment = <"e.g. Local hospital departmental infomation or additional metadata to align with HL7 or CDISC equivalents.">
				>
			>
		>
	>
